SET(EDB_QMYSQL_SOURCE_PATH "${EDB_QT_PLUGINS_SOURCE_PATH}/sqldrivers/mysql")

SET(EDB_BUILD_MARIADB_CONNECTOR_DEFAULT OFF)
IF(WIN32)
    SET(EDB_BUILD_MARIADB_CONNECTOR_DEFAULT ON)
ENDIF()

MESSAGE(STATUS "Building QMYSQL driver with external MySQL/MariaDB connector.")

FIND_LIBRARY(MYSQL_LIB_PATH NAMES mariadbclient libmariadbclient mysqlclient libmysqlclient)
FIND_PATH(MYSQL_INCLUDE_DIR mysql.h PATH_SUFFIXES mariadb mysql)

LIST(APPEND EDB_QMYSQL_LIBRARIES "${MYSQL_LIB_PATH}")
LIST(APPEND EDB_QMYSQL_INCLUDES "${MYSQL_INCLUDE_DIR}")

IF(WIN32)
    LIST(APPEND EDB_QMYSQL_LIBRARIES crypt32 secur32 shlwapi ws2_32)
ENDIF()

ADD_LIBRARY(qsqlmysql SHARED
        "${EDB_QMYSQL_SOURCE_PATH}/qsql_mysql.cpp"
        "${EDB_QMYSQL_SOURCE_PATH}/main.cpp")
SET_TARGET_PROPERTIES(qsqlmysql PROPERTIES AUTOMOC ON)
TARGET_LINK_LIBRARIES(qsqlmysql
        Qt${QT_VERSION_MAJOR}::Core
        Qt${QT_VERSION_MAJOR}::Sql
        Qt${QT_VERSION_MAJOR}::SqlPrivate
        "${EDB_QMYSQL_LIBRARIES}"
        )
TARGET_COMPILE_DEFINITIONS(qsqlmysql PRIVATE
        QT_PLUGIN
        )
TARGET_INCLUDE_DIRECTORIES(qsqlmysql PRIVATE "${EDB_QMYSQL_INCLUDES}")
SET_PROPERTY(TARGET qsqlmysql PROPERTY RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin/plugins/sqldrivers")
SET_PROPERTY(TARGET qsqlmysql PROPERTY LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin/plugins/sqldrivers")

LIST(APPEND EDB_QT_EXTRA_PLUGIN_TARGETS qsqlmysql)
PROPAGATE_PARENT_SCOPE(EDB_QT_EXTRA_PLUGIN_TARGETS)


INSTALL(TARGETS qsqlmysql
        RUNTIME DESTINATION bin/plugins/sqldrivers
        LIBRARY DESTINATION bin/plugins/sqldrivers
        )
